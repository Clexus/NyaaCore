import java.nio.charset.StandardCharsets

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.papermc.paperweight.userdev' version '1.3.8'
    id 'xyz.jpenilla.run-paper' version '1.0.6' // Adds runServer and runMojangMappedServer tasks for testing
}


ext.pluginNameUpper = "NyaaCore"
ext.pluginNameLower = ext.pluginNameUpper.toLowerCase()

ext.majorVersion = 9
ext.minorVersion = 1
ext.minecraftVersion = "1.19.2"


// Supplied by Jenkins
ext.buildNumber = System.env.BUILD_NUMBER == null ? "x" : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"
ext.jdDirectory = System.env.JAVADOCS_DIR == null ? null : "$System.env.JAVADOCS_DIR"

// Version used for distribution. Different from maven repo
group = "cat.nyaa"
archivesBaseName = "${pluginNameUpper}-mc$minecraftVersion"
version = "$majorVersion.$minorVersion.$buildNumber".toString()

// extra compile warnings
compileJava {
    options.compilerArgs += ["-Xlint:deprecation"]
    options.encoding = StandardCharsets.UTF_8.name()
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

assemble {
    dependsOn(reobfJar)
}

repositories {
    mavenCentral()
    maven { name 'Bugjang'; url 'https://libraries.minecraft.net' }
    maven { name 'Spigot'; url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name 'Sonatype'; url 'https://oss.sonatype.org/content/groups/public' }
    maven { name "essentialsx-releases"; url "https://repo.essentialsx.net/releases/" }
    maven { name "papermc"; url "https://papermc.io/repo/repository/maven-public/" }
    maven { name 'NyaaCat'; url 'https://ci.nyaacat.com/maven/' }
}

dependencies {

    testImplementation 'junit:junit:4.13.2'
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:$minecraftVersion-R0.1-SNAPSHOT")


    // other plugins
    compileOnly('net.essentialsx:EssentialsX:2.19.6')      // soft dep

    compileOnly 'org.jetbrains:annotations:23.0.0'
    // Testing

    testImplementation(platform('org.junit:junit-bom:5.9.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
//    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.19:2.119.3'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.8.0'
    testImplementation 'org.xerial:sqlite-jdbc:3.39.3.0'
    testImplementation 'ch.vorburger.mariaDB4j:mariaDB4j:2.5.3'
}


task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.java.srcDirs
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}


processResources { // modify version string
    filteringCharset = StandardCharsets.UTF_8.name()
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
    filesMatching("**/MCVersion") {
        expand 'minecraftVersion': project.minecraftVersion
    }
//    from(deobfMappingCsrg) {
//        rename { 'spigot-deobf.csrg' }
//    }
}
processTestResources {
//    from(deobfMappingCsrg) {
//        rename { 'spigot-deobf.csrg' }
//    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            group project.group
            artifactId pluginNameLower
            version "$majorVersion.$minorVersion-SNAPSHOT"

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url mavenDirectory
        }
    }
}

javadoc {
    // javadoc output folder
    if (project.jdDirectory != null) destinationDir = file("${jdDirectory}/${pluginNameLower}-${version}")

    (options as StandardJavadocDocletOptions).with {
        links 'https://docs.oracle.com/en/java/javase/16/docs/api/'
        //links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://guava.dev/releases/21.0/api/docs/'
        links 'https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/'

        locale 'en_US'
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        addBooleanOption('keywords', true)
        addStringOption('Xdoclint:none', '-quiet')

        options.addBooleanOption('html5', true)

        windowTitle = "${pluginNameUpper} Javadoc"
        docTitle = "${pluginNameUpper} (mc$minecraftVersion-${project.version})"
    }
}
